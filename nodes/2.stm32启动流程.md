# 一、硬件复位后，根据BOOT引脚决定启动方式

复位后，在 SYSCLK 的第四个上升沿锁存 BOOT 引脚的值。**BOOT0 为专用引脚，而 BOOT1 则与 GPIO 引脚共用。**一旦完成对 BOOT1 的采样，相应 GPIO 引脚即进入空闲状态，可用于其它用途。BOOT0与BOOT1引脚的不同值指向了三种启动方式：

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/a320bac9addf427997ac10f8079c572e.png)

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/fd5e6a2207e9467096561373d0654ea0.png)

## 1.从主Flash启动

主Flash指的是STM32的内置Flash。选择该启动模式后，`内置Flash的起始地址将被重映射到0x00000000地址`，代码将在该处开始执行。一般我们使用`JTAG或者SWD模式下载调试程序时，就是下载到这里面，重启后也直接从这启动`。



## 2.从系统存储器启动

系统储存器指的是`STM32的内置ROM`，选择该启动模式后，内置ROM的起始地址将被重映射到0x00000000地址，代码在此处开始运行。`ROM中有一段出厂预置的代码，这段代码起到一个桥的作用，允许外部通过UART/CAN或USB等将代码写入STM32的内置Flash中`。这段代码也被称为`ISP(In System Programing)`代码，这种烧录代码的方式也被称为`ISP烧录`。



## 3.从嵌入式SRAM中启动

显然，该方法是在STM32的内置`SRAM`中启动，选择该启动模式后，内置SRAM的起始地址将被重映射到0x00000000地址，代码在此处开始运行。`这种模式由于烧录程序过程中不需要擦写Flash，因此速度较快，适合调试，但是掉电丢失`。



# 二、启动代码执行流程

这里我们主要分析，从stm32内置的flash中开始启动。



## 1.内存映射

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/e1d57ec6a216425abe71dbcc62d336b7.png)



## 2.Keil 汇编语法

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/6999ab52dddc437d96ead58322a37509.png)



## 3.启动文件分析



### (1)栈空间和堆空间的开辟

```assembly
; Amount of memory (in bytes) allocated for Stack
; Tailor this value to your application needs
; <h> Stack Configuration
;   <o> Stack Size (in Bytes) <0x0-0xFFFFFFFF:8>
; </h>

;等价于C语言的#define
Stack_Size      EQU     0x400

;AREA定义一个新的代码段或者数据段,段名为STACK，NOINIT 表示不初始化
;READWRITE 表示可读可写；ALIGN=3，表示按照 2^3对齐，即 8 字节对齐。
                AREA    STACK, NOINIT, READWRITE, ALIGN=3
                
;SPACE分配内存指令，分配大小为Stack_Size字节连续的存储单元给栈空间。
Stack_Mem       SPACE   Stack_Size

;__initial_sp紧挨着SPACE放置，表示栈的结束地址，栈是从高往低生长，所以结束地址就是栈顶地址。
__initial_sp


; <h> Heap Configuration
;   <o>  Heap Size (in Bytes) <0x0-0xFFFFFFFF:8>
; </h>

;堆的大小为 0x00000200，也就是512字节
Heap_Size       EQU     0x200

                AREA    HEAP, NOINIT, READWRITE, ALIGN=3

;堆空间的开始地址                
__heap_base

;堆空间大小分配
Heap_Mem        SPACE   Heap_Size

;堆空间结束地址,堆是从低到高生长，跟栈的生长方向相反
__heap_limit

;PRESERVE8：指示编译器按照8字节对齐。
;THUMB：指示编译器之后的指令为THUMB指令
                PRESERVE8
                THUMB
                
|  高              
|
|----------__heap_limit
| (堆区)512Byte          
|--------- __initial_sp / __heap_base
| (栈区)1KB
|  低

```

#### 栈空间基地址确定

通过代码我们可以很容易的知道堆栈的大小，但是它们的绝对地址或者说基地址仅仅从这里是得不到的。

编译器编译完工程后，根据生成.**==bss段（ZI-data）和.data段（RW-data）==的大小以及==RAM的起始地址==，来计算==堆栈的基地址==**。

<font color=green>__initial_sp = **SRAM Base Addr** + **RW-data** + **ZI-data**</font>

```assembly
Build started: Project: stm32f103c8t6
*** Using Compiler 'V6.19', folder: 'C:\Keil_v5\ARM\ARMCLANG\Bin'
Build target 'stm32f103c8t6'
compiling main.c...
linking...
Program Size: Code=17700 RO-data=388 RW-data=16 ZI-data=2752  
FromELF: creating hex file...
"stm32f103c8t6\stm32f103c8t6.axf" - 0 Error(s), 0 Warning(s).
Build Time Elapsed:  00:00:05
-------------------------------------------------------------------------------------
其中Code、RO-data、RW-data和ZI-data是什么意思呢？
01：Code 代表执行的代码，程序中所有的函数；
02：RO-data 代表只读数据，程序中所定义的常量数据；
03：RW-data代表已初始化的读写数据，程序中定义并且初始化的全局变量和静态变量；
04：ZI-data代表定义了但未初始化的可读写数据，
    ZI英语是zero initial，就是程序中用到的变量并且被系统初始化为0的变量的字节数，
    keil编译器默认是把你没有初始化的变量都赋值一个0，这些变量在程序运行时是保存在RAM中的。

```



### (2)中断向量表定义

```assembly
; Vector Table Mapped to Address 0 at Reset
; 定义一个数据段，名字为RESET, READONLY表示只读。
; EXPORT表示声明一个标号具有全局属性，可被外部的文件使用。
; 这里是声明了__Vectors、__Vectors_End和__Vectors_Size三个标号具有全局性，可被外部的文件使用。
                AREA    RESET, DATA, READONLY
                EXPORT  __Vectors
                EXPORT  __Vectors_End
                EXPORT  __Vectors_Size
                
 ;  __Vectors 记录中断向量表开始的位置     
 ;DCD 表示分配 1 个 4 字节的空间。中断向量表存放的实际上是中断服务程序的入口地址。
 ;当异常（也即是中断事件）发生时，CPU的中断系统会将相应的入口地址赋值给PC寄存器之后
 ;就开始执行中断服务程序。         
__Vectors       DCD     __initial_sp               ; Top of Stack
                DCD     Reset_Handler              ; Reset Handler
                DCD     NMI_Handler                ; NMI Handler
                DCD     HardFault_Handler          ; Hard Fault Handler
                DCD     MemManage_Handler          ; MPU Fault Handler
                DCD     BusFault_Handler           ; Bus Fault Handler
                DCD     UsageFault_Handler         ; Usage Fault Handler
                DCD     0                          ; Reserved
                DCD     0                          ; Reserved
                DCD     0                          ; Reserved
                DCD     0                          ; Reserved
                DCD     SVC_Handler                ; SVCall Handler
                DCD     DebugMon_Handler           ; Debug Monitor Handler
                DCD     0                          ; Reserved
                DCD     PendSV_Handler             ; PendSV Handler
                DCD     SysTick_Handler            ; SysTick Handler
                
; External Interrupts
                DCD     WWDG_IRQHandler            ; Window Watchdog
                DCD     PVD_IRQHandler             ; PVD through EXTI Line detect
                DCD     TAMPER_IRQHandler          ; Tamper
                DCD     RTC_IRQHandler             ; RTC
                DCD     FLASH_IRQHandler           ; Flash
                DCD     RCC_IRQHandler             ; RCC
                DCD     EXTI0_IRQHandler           ; EXTI Line 0
                DCD     EXTI1_IRQHandler           ; EXTI Line 1
                DCD     EXTI2_IRQHandler           ; EXTI Line 2
                DCD     EXTI3_IRQHandler           ; EXTI Line 3
                DCD     EXTI4_IRQHandler           ; EXTI Line 4
                DCD     DMA1_Channel1_IRQHandler   ; DMA1 Channel 1
                DCD     DMA1_Channel2_IRQHandler   ; DMA1 Channel 2
                DCD     DMA1_Channel3_IRQHandler   ; DMA1 Channel 3
                DCD     DMA1_Channel4_IRQHandler   ; DMA1 Channel 4
                DCD     DMA1_Channel5_IRQHandler   ; DMA1 Channel 5
                DCD     DMA1_Channel6_IRQHandler   ; DMA1 Channel 6
                DCD     DMA1_Channel7_IRQHandler   ; DMA1 Channel 7
                DCD     ADC1_2_IRQHandler          ; ADC1_2
                DCD     USB_HP_CAN1_TX_IRQHandler  ; USB High Priority or CAN1 TX
                DCD     USB_LP_CAN1_RX0_IRQHandler ; USB Low  Priority or CAN1 RX0
                DCD     CAN1_RX1_IRQHandler        ; CAN1 RX1
                DCD     CAN1_SCE_IRQHandler        ; CAN1 SCE
                DCD     EXTI9_5_IRQHandler         ; EXTI Line 9..5
                DCD     TIM1_BRK_IRQHandler        ; TIM1 Break
                DCD     TIM1_UP_IRQHandler         ; TIM1 Update
                DCD     TIM1_TRG_COM_IRQHandler    ; TIM1 Trigger and Commutation
                DCD     TIM1_CC_IRQHandler         ; TIM1 Capture Compare
                DCD     TIM2_IRQHandler            ; TIM2
                DCD     TIM3_IRQHandler            ; TIM3
                DCD     TIM4_IRQHandler            ; TIM4
                DCD     I2C1_EV_IRQHandler         ; I2C1 Event
                DCD     I2C1_ER_IRQHandler         ; I2C1 Error
                DCD     I2C2_EV_IRQHandler         ; I2C2 Event
                DCD     I2C2_ER_IRQHandler         ; I2C2 Error
                DCD     SPI1_IRQHandler            ; SPI1
                DCD     SPI2_IRQHandler            ; SPI2
                DCD     USART1_IRQHandler          ; USART1
                DCD     USART2_IRQHandler          ; USART2
                DCD     USART3_IRQHandler          ; USART3
                DCD     EXTI15_10_IRQHandler       ; EXTI Line 15..10
                DCD     RTC_Alarm_IRQHandler       ; RTC Alarm through EXTI Line
                DCD     USBWakeUp_IRQHandler       ; USB Wakeup from suspend
;记录中断向量表结束的位置
__Vectors_End


;计算中断向量表大小
__Vectors_Size  EQU  __Vectors_End - __Vectors

```

**中断向量表位于代码段的最前面**，

- <font color=green>**当硬件复位时，自动从0x0800 0000位置处读取数据赋给栈指针SP**</font>

- <font color=green>**然后自动从0x08000004位置处读取数据赋给PC(即执行Reset_Handler)**。</font>

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/a8645106c3e440b5ab2aa406b9f6f20b.png)

#### 调试验证

![在这里插入图片描述](https://img-blog.csdnimg.cn/direct/2c6ebb4b30164af0bca385405fa7c353.png)
![在这里插入图片描述](https://img-blog.csdnimg.cn/direct/892ee028432343a0a2ac35154ad4ed15.png)

### (3)Reset_Handler处理

```assembly
;AREA 定义一块代码段，只读，段名字是 .text 。READONLY 表示只读
                AREA    |.text|, CODE, READONLY
                
;Reset handler
;利用 PROC、ENDP 这一对伪指令把程序段分为若干个过程，使程序的结构加清晰
;WEAK 声明其他的同名标号优先于该标号被引用,就是说如果外面声明了的话会调用外面的。
;这个声明很重要，它让我们可以在 C 文件中任意地方放置中断服务程序，
;只要保证 C 函数的名字和向量表中的名字一致即可。
Reset_Handler    PROC
                 EXPORT  Reset_Handler             [WEAK]
 
;IMPORT：伪指令用于通知编译器要使用的标号在其他的源文件中定义。类似C语言extern关键字功能
;但要在当前源文件中引用，而且无论当前源文件是否引用该标号，该标号均会被加入到当前源文件的符号表中。
     IMPORT  __main
     IMPORT  SystemInit
     
;SystemInit()是一个标准的库函数，在 system_stm32f1xx.c这个库文件总定义。主要作用是配置系统时钟
                 LDR     R0, =SystemInit
                 BLX     R0
                 
;__main函数属于ARMCC编译器提供的库函数,该函数的作用是
;1.完成全局/静态变量的初始化工作(清BSS段)
;2.初始化堆栈
;3.库函数的初始化
;4.程序的跳转，进入main()函数。
                 LDR     R0, =__main
                 BX      R0
                 ENDP
```

#### SystemInit函数实现

```c
/**
  * @brief  Setup the microcontroller system
  *         Initialize the Embedded Flash Interface, the PLL and update the 
  *         SystemCoreClock variable.
  * @note   This function should be used only after reset.
  * @param  None
  * @retval None
  */
void SystemInit (void)
{
#if defined(STM32F100xE) || defined(STM32F101xE)\
 || defined(STM32F101xG) || defined(STM32F103xE) || defined(STM32F103xG)
  #ifdef DATA_IN_ExtSRAM
    SystemInit_ExtMemCtl(); 
  #endif /* DATA_IN_ExtSRAM */
#endif 

  /* Configure the Vector Table location -------------------------------------*/
#if defined(USER_VECT_TAB_ADDRESS)
  /* Vector Table Relocation in Internal SRAM. */
  SCB->VTOR = VECT_TAB_BASE_ADDRESS | VECT_TAB_OFFSET; 
#endif /* USER_VECT_TAB_ADDRESS */
}

```



### (4)其他中断异常处理

```assembly
; Dummy Exception Handlers (infinite loops which can be modified)
;这些中断异常处理函数时默认的函数，并且如果在C语言中重新实现了这些函数，当中断异常产生的时候就调用
;在C语言中重新实现的中断异常处理函数
NMI_Handler     PROC
                EXPORT  NMI_Handler                [WEAK]
                ;死循环
                B       .
                ENDP
HardFault_Handler\
                PROC
                EXPORT  HardFault_Handler          [WEAK]
                B       .
                ENDP
MemManage_Handler\
                PROC
                EXPORT  MemManage_Handler          [WEAK]
                B       .
                ENDP
BusFault_Handler\
                PROC
                EXPORT  BusFault_Handler           [WEAK]
                B       .
                ENDP
UsageFault_Handler\
                PROC
                EXPORT  UsageFault_Handler         [WEAK]
                B       .
                ENDP
SVC_Handler     PROC
                EXPORT  SVC_Handler                [WEAK]
                B       .
                ENDP
DebugMon_Handler\
                PROC
                EXPORT  DebugMon_Handler           [WEAK]
                B       .
                ENDP
PendSV_Handler  PROC
                EXPORT  PendSV_Handler             [WEAK]
                B       .
                ENDP
SysTick_Handler PROC
                EXPORT  SysTick_Handler            [WEAK]
                B       .
                ENDP

Default_Handler PROC

                EXPORT  WWDG_IRQHandler            [WEAK]
                EXPORT  PVD_IRQHandler             [WEAK]
                EXPORT  TAMPER_IRQHandler          [WEAK]
                EXPORT  RTC_IRQHandler             [WEAK]
                EXPORT  FLASH_IRQHandler           [WEAK]
                EXPORT  RCC_IRQHandler             [WEAK]
                EXPORT  EXTI0_IRQHandler           [WEAK]
                EXPORT  EXTI1_IRQHandler           [WEAK]
                EXPORT  EXTI2_IRQHandler           [WEAK]
                EXPORT  EXTI3_IRQHandler           [WEAK]
                EXPORT  EXTI4_IRQHandler           [WEAK]
                EXPORT  DMA1_Channel1_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel2_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel3_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel4_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel5_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel6_IRQHandler   [WEAK]
                EXPORT  DMA1_Channel7_IRQHandler   [WEAK]
                EXPORT  ADC1_2_IRQHandler          [WEAK]
                EXPORT  USB_HP_CAN1_TX_IRQHandler  [WEAK]
                EXPORT  USB_LP_CAN1_RX0_IRQHandler [WEAK]
                EXPORT  CAN1_RX1_IRQHandler        [WEAK]
                EXPORT  CAN1_SCE_IRQHandler        [WEAK]
                EXPORT  EXTI9_5_IRQHandler         [WEAK]
                EXPORT  TIM1_BRK_IRQHandler        [WEAK]
                EXPORT  TIM1_UP_IRQHandler         [WEAK]
                EXPORT  TIM1_TRG_COM_IRQHandler    [WEAK]
                EXPORT  TIM1_CC_IRQHandler         [WEAK]
                EXPORT  TIM2_IRQHandler            [WEAK]
                EXPORT  TIM3_IRQHandler            [WEAK]
                EXPORT  TIM4_IRQHandler            [WEAK]
                EXPORT  I2C1_EV_IRQHandler         [WEAK]
                EXPORT  I2C1_ER_IRQHandler         [WEAK]
                EXPORT  I2C2_EV_IRQHandler         [WEAK]
                EXPORT  I2C2_ER_IRQHandler         [WEAK]
                EXPORT  SPI1_IRQHandler            [WEAK]
                EXPORT  SPI2_IRQHandler            [WEAK]
                EXPORT  USART1_IRQHandler          [WEAK]
                EXPORT  USART2_IRQHandler          [WEAK]
                EXPORT  USART3_IRQHandler          [WEAK]
                EXPORT  EXTI15_10_IRQHandler       [WEAK]
                EXPORT  RTC_Alarm_IRQHandler        [WEAK]
                EXPORT  USBWakeUp_IRQHandler       [WEAK]

WWDG_IRQHandler
PVD_IRQHandler
TAMPER_IRQHandler
RTC_IRQHandler
FLASH_IRQHandler
RCC_IRQHandler
EXTI0_IRQHandler
EXTI1_IRQHandler
EXTI2_IRQHandler
EXTI3_IRQHandler
EXTI4_IRQHandler
DMA1_Channel1_IRQHandler
DMA1_Channel2_IRQHandler
DMA1_Channel3_IRQHandler
DMA1_Channel4_IRQHandler
DMA1_Channel5_IRQHandler
DMA1_Channel6_IRQHandler
DMA1_Channel7_IRQHandler
ADC1_2_IRQHandler
USB_HP_CAN1_TX_IRQHandler
USB_LP_CAN1_RX0_IRQHandler
CAN1_RX1_IRQHandler
CAN1_SCE_IRQHandler
EXTI9_5_IRQHandler
TIM1_BRK_IRQHandler
TIM1_UP_IRQHandler
TIM1_TRG_COM_IRQHandler
TIM1_CC_IRQHandler
TIM2_IRQHandler
TIM3_IRQHandler
TIM4_IRQHandler
I2C1_EV_IRQHandler
I2C1_ER_IRQHandler
I2C2_EV_IRQHandler
I2C2_ER_IRQHandler
SPI1_IRQHandler
SPI2_IRQHandler
USART1_IRQHandler
USART2_IRQHandler
USART3_IRQHandler
EXTI15_10_IRQHandler
RTC_Alarm_IRQHandler
USBWakeUp_IRQHandler

                B       .

                ENDP

                ALIGN

```



### (5)用户栈和堆的初始化

```assembly
;*******************************************************************************
; User Stack and Heap initialization
;*******************************************************************************
;简单的汇编语言实现 IF…….ELSE…………语句。如果定义了 MICROLIB，那么程序是不会执行 ELSE分支的代码。
;__MICROLIB 可能大家并不陌生，就在 MDK 的 Target Option 里面设置。
                 IF      :DEF:__MICROLIB
                
                 EXPORT  __initial_sp
                 EXPORT  __heap_base
                 EXPORT  __heap_limit
                
                 ELSE
                
                 IMPORT  __use_two_region_memory
                 EXPORT  __user_initial_stackheap
                 
__user_initial_stackheap

                 LDR     R0, =  Heap_Mem
                 LDR     R1, = (Stack_Mem + Stack_Size)
                 LDR     R2, = (Heap_Mem +  Heap_Size)
                 LDR     R3, = Stack_Mem
                 BX      LR

                 ALIGN

                 ENDIF

                 END
```

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/1b8bc3639a804466baafd9bb63f0c3f1.png)

#### MicroLib

`MicroLib是一个针对用C编写的基于ARM的嵌入式应用程序的高度优化的库`。**与包含在ARM编译器工具链中的标准C库相比，`MicroLib提供了许多嵌入式系统所需的代码大小的显著优势`**。

如果在stm32中使用**printf通过串口输出信息要勾选MicroLib**，否则不能正常输出信息。

而且有些应用中**如果用了 STM32 的浮点运算单元 FPU，一定要同时开微库**，不然有时会出现各种奇怪的现象。

#### 如果勾选 MicroLIB 后  build 报错

```c
linking...
Stm32SmartCar\Stm32SmartCar.axf: Error: L6218E: Undefined symbol __use_two_region_memory
                                 (referred from startup_stm32f103xb.o).
Stm32SmartCar\Stm32SmartCar.axf: Error: L6218E: Undefined symbol __initial_sp
                                 (referred from entry2.o).
Not enough information to list image symbols.
Not enough information to list load addresses in the image map.
Finished: 2 information, 0 warning and 2 error messages.
"Stm32SmartCar\Stm32SmartCar.axf" - 2 Error(s), 0 Warning(s).
Target not created.
Build Time Elapsed:  00:00:01
代码块1234567891011
```

需要重新 rebuild 一下:

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/440bdc41aa0e4ccaab53187b33ba2174.png)

> 因为 keil 在你点击编译的时候只会编译内容有变动的部分(指预处理后的部分). **当你选择使用 MicroLIB 的时候, 编译器会自动添加宏定义`__MICROLIB`,** 相对应的, 头文件中的内容就会有所改变, 导致包含了头文件的 .c 文件内容改变. 而 startup 中处存在的条件汇编语句却没有得到预处理和汇编: 被抛弃的部分是堆栈的初始化处理程序, 可以看出定义了`__MICROLIB`宏和没有定义时堆栈的初始化是交由不同的函数处理的. 但是编译的时候没改变这里的条件汇编, 结果没有初始化的程序了, 因此报错"符号未定义". 解决方式同样很简单, <font color="#fd6f01">**点击重新编译即可.**</font>



# 三、启动流程总结

MCU上电后<font color=green>**从0x0800 0000处读取栈顶地址并保存，然后从0x0800 0004读取中断向量表的起始地址，这就是复位程序的入口地址**</font>，

<font color=green>**接着跳转到复位程序入口处，初始向量表，然后设置时钟，设置堆栈，最后跳转到main函数**</font>，即进入用户程序。

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/fc206c6baa37411f98bd48aa073b6e4a.png)